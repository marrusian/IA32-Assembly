TITLE Str_nextword Procedure				(_strNextword.asm)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
; Description:	 Implements and tests a string-handling procedure that
;				 scans a string for the first occurence of a certain
;				 delimiter character and replaces the delimiter with
;				 the null byte.
; Author:		 Marrusian
; Creation Date: 14/08/2014
; Revisions:	 N/A
; Date:			 N/A
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
INCLUDELIB	D:\masm32\lib\Irvine32.lib
				.386
				.MODEL flat,stdcall
				.STACK 100h
Str_nextword	PROTO str1:PTR BYTE,char:BYTE
Crlf			PROTO
WriteString		PROTO
ExitProcess		PROTO dwExitCode:DWORD
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.DATA
target			BYTE "Johnson,Calvin",0
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.CODE
main			PROC
				INVOKE Str_nextword, ADDR target, ','
				jnz skip
				mov edx,eax
				call WriteString
				call Crlf
skip:			mov edx,OFFSET target
				call WriteString
				call Crlf
				INVOKE ExitProcess,0
main			ENDP

; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
Str_nextword	PROC USES edi,
					 str1:PTR BYTE, char:BYTE
;
; Description:	Scans a string for the first occurrence of a certain delimiter
;				character and replaces the delimiter with a null byte.
; Receives:		str1:PTR BYTE	=	points to the string
;				char:BYTE		=	the delimiter character
; Returns:		Case I - Delimiter Found:
;				EAX = offset of the next character beyond the delimiter
;				ZF = 1
;				Case II - Not Found:
;				EAX = undefined, ZF = 0
; Calls:		N/A
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				mov edi,str1
				mov al,char
				cld
while01:		cmp BYTE PTR [edi],0
				jz endw01
				scasb
				jnz endscan
				mov eax,edi
				mov BYTE PTR [edi-1],0
				jmp Found
endscan:		jmp while01
endw01:			or edi,1
Found:			ret
Str_nextword	ENDP

				END main