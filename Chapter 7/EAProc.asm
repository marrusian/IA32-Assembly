TITLE Extended Addition			(EAProc.asm)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
; Description:	 Adds two extended integers of the same size.
; Author:		 Marrusian
; Creation Date: 09/08/2014
; Revisions:	 N/A
; Date:			 N/A
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
INCLUDELIB	D:\masm32\lib\Irvine32.lib
				.386
				.MODEL flat,stdcall
				.STACK 100h
Crlf			PROTO
WriteHexB		PROTO
ExitProcess		PROTO dwExitCode:DWORD
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.DATA
op1				DWORD 74981234h, 0A2B2A406h, 0CBF2A1DDh, 1203A4BBh  
op2				DWORD 00234502h, 80108700h, 0ABCDEF10h, 50ABC2DDh
sum				BYTE (SIZEOF op1)+1	DUP(?)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.CODE
main			PROC
				mov esi,OFFSET op1
				mov edi,OFFSET op2
				mov ebx,OFFSET sum
				mov ecx,SIZEOF op1
				call Extended_Add
				mov esi,OFFSET sum
				mov ecx,SIZEOF sum
				call Display_Sum
				call Crlf
				INVOKE ExitProcess,0
main			ENDP

; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
Extended_Add	PROC USES esi edi ebx ecx
;
; Description:	Calculates the sum of two extended integers stored as
;				arrays of bytes.
; Receives:		ESI = the offset of first integer
;				EDI = the offset of second integer
;				EBX = the offset of the variable which will hold the sum
;				ECX = number of bytes to be added
; Preconds:		The length of the sum variable must be one byte longer
;				than the input operands.
; Returns:		N/A
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				clc
L1:				mov al,[esi]
				adc al,[edi]
				mov [ebx],al
				inc esi
				inc edi
				inc ebx
				loop L1
				mov BYTE PTR [ebx],0
				adc BYTE PTR [ebx],0
				ret
Extended_add	ENDP

; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤

Display_Sum		PROC USES esi ebx ecx
				add esi,ecx
				sub esi,TYPE BYTE
				mov ebx,TYPE BYTE
L1:				mov al,[esi]
				call WriteHexB
				sub esi,TYPE BYTE
				loop L1
				ret
Display_Sum		ENDP
				END main