TITLE AscAdd Procedure					(AscAdd.asm)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
; Description:	 Adds two ASCII decimal integers of arbitrary size
;				 (both must be the same size).
; Author:		 Marrusian
; Creation Date: 10/08/2014
; Revisions:	 N/A
; Date:			 N/A
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
INCLUDELIB	D:\masm32\lib\Irvine32.lib
				.386
				.MODEL flat,stdcall
				.STACK 100h
Crlf			PROTO
WriteString		PROTO
ExitProcess		PROTO dwExitCode:DWORD
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.DATA			
sInt1			BYTE "192834512"
sInt2			BYTE "485717233"
sSum			BYTE (SIZEOF sInt1)+1 DUP(?),0
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.CODE
main			PROC	
				mov esi,OFFSET sInt1
				mov edi,OFFSET sInt2
				mov edx,OFFSET sSum
				mov ecx,(LENGTHOF sInt1)
				call AscAdd
				call WriteString
				call Crlf
main			ENDP

; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
AscAdd			PROC USES esi edi edx ecx eax
;
; Description:	See the program's description.
; Receives:		ESI = the offset of the first integer
;				EDI = the offset of the second integer
;				EDX = the offset of the variable to hold the sum
;				ECX = the number of digits in the operands
; Returns:		N/A
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				add esi,ecx
				dec esi
				add edi,ecx
				dec edi
				add edx,ecx
				xor bh,bh
for01:			xor ah,ah
				mov al,[esi]
				add al,[edi]
				AAA
				add al,bh
				mov bh,ah
				or al,30h
				mov [edx],al
				dec esi
				dec edi
				dec edx
				loop for01				
				or ah,30h
				mov [edx],ah
				ret
AscAdd			ENDP
				END main 