TITLE Random Screen Locations				(RSLoc.asm)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
; Description:	 Displays a single character at 100 random screen locations,
;				 using a timing delay of 100 milliseconds (10 char/sec).
; Author:		 Marrusian
; Creation Date: 04/08/2014
; Revisions:	 1.0 - Added GenTextClr procedure. Now the character gets
;				 a new color at every new screen location.
; Date:			 04/08/2014
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
includelib	D:\masm32\lib\Irvine32.lib
				.386
				.MODEL flat,stdcall
				.STACK 100h
GetMaxXY		PROTO
Gotoxy			PROTO			
WriteChar		PROTO
Delay			PROTO
Randomize		PROTO	
RandomRange		PROTO
SetTextColor	PROTO
ExitProcess		PROTO dwExitCode:DWORD
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.DATA
RANDOM_COUNTER = 100
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				.CODE
main			PROC
; Calls: Randomize, GenScreenLoc, WriteTimedChar, GenTextClr
				mov ecx,RANDOM_COUNTER
				call Randomize
for01:			push ecx
				call GenScreenLoc
				mov al,'X'
				mov ecx,100
				call WriteTimedChar
				call GenTextClr
				pop ecx
				loop for01
endfor01:		nop
				INVOKE ExitProcess,0
main			ENDP

; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
GenScreenLoc	PROC USES eax edx
;
; Description:	Gets the number of columns and rows in the console window's
;				buffer and then generates a random location inside it.
; Receives:		N/A
; Returns:		N/A
; Calls:		GetMaxXY, Gotoxy, RandomRange					(Irvine32.lib)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				mov eax,0		
				call GetMaxXY			; DL = number of buffer columns, AL = number of buffer rows
				inc eax					; Return value of RandomRange PROC is N-1, so the arg. has to be N+1 to get a return value of N.
				call RandomRange
				mov dh,al				; DH = Y-Coordinate(row)
				mov al,dl
				inc eax
				call RandomRange
				mov dl,al				; DL = X-Coordinate(column)
				call Gotoxy
				ret
GenScreenLoc	ENDP

; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
GenTextClr		PROC
;
; Description:	Generates a random text color out of 16 possibilities.
; Receives:		N/A
; Returns:		N/A
; Calls:		SetTextColor									(Irvine32.lib)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				mov eax,16
				call RandomRange
				call SetTextColor
				ret
GenTextClr		ENDP
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
WriteTimedChar	PROC USES eax ecx
;
; Description:	Write a single character to the console window with a delay
;				of N milliseconds.
; Receives:		AL = the character's ASCII code
;				ECX = the number of milliseconds to be delayed
; Returns:		N/A
; Calls:		WriteChar										(Irvine32.lib)
; ¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
				call WriteChar
				mov eax,ecx
				call Delay
				ret
WriteTimedChar	ENDP
				END main
							